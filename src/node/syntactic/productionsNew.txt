Program->Declarations

Declarations->Declaration|Declaration Declarations

Declaration->VariableDeclaration|FunctionDeclaration

VariableDeclaration->int ID ;

FunctionDeclaration->int ID ( FormalParamList ) BlockStatement|void ID ( FormalParamList ) BlockStatement

FormalParamList->FormalParams|void

FormalParams->Param|Param , FormalParams

Param->int ID

BlockStatement->{ VariableDeclarations ExpressionStatements }|{ ExpressionStatements }

VariableDeclarations->VariableDeclaration|VariableDeclaration VariableDeclarations

ExpressionStatements->ExpressionStatement|ExpressionStatement ExpressionStatements

ExpressionStatement->IfStatement|WhileStatement|ReturnStatement|AssignStatement

AssignStatement->ID = Expression ;

ReturnStatement->return ;|return Expression ;

WhileStatement->while ( Expression ) BlockStatement

IfStatement->if ( Expression ) BlockStatement ElseStatement

ElseStatement->$|else BlockStatement

Expression->AddExpression|AddExpression Relop Expression

Relop-><|<=|>|>=|==|!=

AddExpression->Item|Item + AddExpression|Item - AddExpression

Item->Factor|Factor * Item|Factor / Item

Factor->num|( Expression )|CallExpression|ID

CallExpression->ID ( ActualParamList )

ActualParamList->ActualParams|$

ActualParams->Expression|Expression , ActualParams

ID->identifier